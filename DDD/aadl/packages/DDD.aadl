--DDD.aadl
--simple dual-chamber pacemaker, DDD mode

package ddd_mode 
public
  with BLESS, SI,  --predeclared BLESS properties
   PP;	--imported property set defining pacing settings that control the behavior of the device

annex Typedef {**
	type time is quantity s
**};

thread DDD
  features
  a: in event port;	--atrial signal
  v: in event port;	--ventricular signal
  ap: out event port	--pace atrium 
    {BLESS::Assertion=>"<<AP(now)>>";};
  vp: out event port	--pace ventricle, but not too soon 
    {BLESS::Assertion=>"<<VP(now) and URL(now)>>";};
  as: out event port --non-refractory atrial sense
    {BLESS::Assertion=>"<<AS(now)>>";};
  vs: out event port	--non-refractory ventricular sense
    {BLESS::Assertion=>"<<VS(now)>>";};
  properties
    Dispatch_Protocol => Sporadic;  
    BLESS::Invariant=>"<<LRL(now)>>";
end DDD;

thread implementation DDD.i   
annex BLESS
{**
assert

--lower rate limit, both A and V chambers
--5.1	Lower Rate Limit (LRL)
--  The Lower Rate Limit (LRL) is the number of generator pace pulses 
--  delivered per minute (atrium or ventricle) in the absence of
--    Sensed intrinsic activity. 
--    Sensor-controlled pacing at a higher rate.
--  The LRL is affected in the following ways:
--    1. When Rate Hysteresis is disabled, the LRL shall define the 
--       longest allowable pacing interval.
--    2. In DXX or VXX modes, the LRL interval starts at a ventricular 
--       sensed or paced event
<<LRL:x~time :    
    -- there has been a V-pace or a non-refractory V-sense 
    exists t~time 
      -- within the previous LRL interval
      in (x-#PP::Lower_Rate_Limit_Interval)..x 
      -- in which a heartbeat was sensed, or caused by pacing
      that (vs or vp)@t  >>

--upper rate limit, only ventricle  
--5.2	Upper Rate Limit (URL)
--  The Upper Rate Limit (URL) is the maximum rate at which the paced 
--  ventricular rate will track sensed atrial events.  
--  The URL interval is the minimum time between a ventricular event  
--  and the next ventricular pace.
<<URL:x~time: --a V-pace never happens too soon after either V-sense or V-pace 
  -- applied to vp: out event port, no pace (out event) too soon, URL is true when event is sent 
  not -- there must not be V-sense or -pace not in previous URL open-interval ,, 
   (exists tu~time 
      in (x-#PP::Upper_Rate_Limit_Interval),,x 
      that (vs or vp)@tu) >>       

--atrial-ventricular delay
--5.3	Atrial-Ventricular (AV) Delay
--  The AV delay shall be the programmable time period from an 
--  atrial event (either intrinsic or paced) to a ventricular pace.
-- In atrial tracking modes, ventricular pacing shall occur in the 
--  absence of a sensed ventricular event within the programmed AV delay 
--  when the sensed atrial rate is between the programmed LRL and URL.
--  AV delay shall either be 
--    1. Fixed (absolute time) 
--    2. Dynamic

--paced AV delay
--5.3.1	Paced AV Delay
--  A paced AV (PAV) delay shall occur when the AV delay is initiated 
--  by an atrial pace.
<<PAV:x~time:	--there has been A-pace during the fixed AV delay
  exists ts~time 
       --in the previous fixed AV delay milliseconds,
      in (x-#PP::Fixed_AV_Delay)..x 
      that ap@ts >> 

--sensed AV delay
--5.3.2	Sensed AV Delay
--  A sensed AV (SAV) delay shall occur when the AV delay is initiated 
--  by an atrial sense.
--Sensed AV Delay Offset is always negative, then added to the
--Fixed AV Delay to determine Sensed AV Delay 
<<SAV:x~time: 
  (exists ta~time 
       --in the previous fixed AV delay milliseconds,
      in (x-#PP::Sensed_AV_Delay)..x 
      that as@ta)  >>
--  or  --a previous V-event prevents pacing above URL
--  (exists tu~time
--    in x-#PP::Upper_Rate_Limit_Interval..x
--    that (vs or vp)@tu)  >>
    
--vetricular refractory period:  
--5.4.1	Ventricular Refractory Period (VRP)
--  The Ventricular Refractory Period shall be the programmed time 
--  interval following a ventricular event during which time 
--  ventricular senses shall not inhibit nor trigger pacing.
<<VRP:x~time:
  exists tv~time
	in (x-#PP::Ventricular_Refractory_Period),,x
	that (vs or vp)@tv  >>  
 
--atrial refractory period
--5.4.2	Atrial Refractory Period (ARP)
--  For single chamber atrial modes, the Atrial Refractory Period (ARP) 
--  shall be the programmed time interval following an atrial event 
--  during which time atrial events shall not inhibit nor trigger pacing.
<<ARP:x~time:
  exists tar~time
  	in (x-#PP::Atrial_Refractory_Period),,x
  	that (as or ap)@tar>>

--post-ventricular atrial refractory period
--5.4.3	Post Ventricular Atrial Refractory Period (PVARP)
--  The Post Ventricular Atrial Refractory Period shall be available 
--  in modes with ventricular pacing and atrial sensing. The Post 
--  Ventricular Atrial Refractory Period shall be the programmable 
--  time interval following a ventricular event when an atrial 
--  cardiac event shall not 
--    1. Inhibit an atrial pace. 
--    2. Trigger a ventricular pace.
<<PVARP:x~time: 
  exists tv~time 
    in (x-#PP::PVARP),,x
    that (vs or vp)@tv >>

--non-refractory ventricular sense occurs, asserted by port vs 
--  there is a ventricular sense, and the ventricular refractory period
--  has expired since previous vs or vp
<<VS:x~time: v@x and not VRP(x) >>
    
--non-refractory atrial sense occurs, asserted by port as 
--  there is an atrial sense,
--  and the atrial refractory period has expired since previous as or ap
--  and the post-ventricular atrial refractory period has expired
--  since previous vs or vp
<<AS:x~time: 
  a@x  --atrial sense at time x
  and not PVARP(x)  --not post-ventricular atrial refractory
  and not ARP(x) >> --not atrial refractory period

--ventricular pace 
--  for DDD, vp! means either
--  pace_now occurs
--  or  either vp or vs occurred LRL interval ago and not since, 
--  or ap occurred paced AV delay ago (coincident with pace at LRL), or
--  or as occurred sensed AV delay ago
--    and no ventricular sense or pace
--    occurred in the previous upper rate limit interval
<<VP:x~time:
	PACE_ON_LRL(x)  -- (vp or vs) occurred LRL interval ago
  or  --as occurred sensed AV delay ago, but not too fast
  PACE_ON_SAV_DELAY(x) >>    
    
<<PACE_ON_LRL:x~time:    --no intrinsic activity, pace at LRL
  (vp or vs)@(x-#PP::Lower_Rate_Limit_Interval)
      and --and not since
      not (exists t~time
        in (x-#PP::Lower_Rate_Limit_Interval),,x  
        --with a non-refractory ventricular sense or pace
        that (vs or vp)@t) >>
                
<<PACE_ON_SAV_DELAY:x~time:	--track atrial sense
    as@(x-#PP::Sensed_AV_Delay) 
      and  --there have been no ventricular events in the upper-rate interval
      not (exists tu~time
        in (x-#PP::Upper_Rate_Limit_Interval),,x 
        that (vs or vp)@tu ) >>  

--atrial pace
--  for DDD, ap! means 
--  the previous vs or vp occured VA interval perviously
--    (VA interval = LRL interval - PAV delay)
--  and there have been no ventricular senses since then
--  nor atrial pace or sense since LRL
<<AP:x~time:  --time out no activity since last ventricular sense or pace
    (vp or vs)@(x-va_interval)
      and --and not since
      not (exists tv~time
        in (x-va_interval),,x  
        --with a ventricular or atrial event
        that (vs or vp or as or ap)@tv) >>
          
--invariant of internal variables
<<VARS:x~time: LAST_VP_OR_VS_A(x) and LAST_AP_OR_AS_A(x) >>

--interactive limit that LRL<URL 
<<AXIOM_LRLi_gt_URLi_LIMIT:x~time:
  (x-#PP::Lower_Rate_Limit_Interval) < (x-#PP::Upper_Rate_Limit_Interval) >>

--A-event occurred before V-event
<<AXIOM_A_B4_V: : last_ap_or_as <= -- ((#PP::Fixed_AV_Delay+now) - #PP::Lower_Rate_Limit_Interval) >>
    (now - va_interval)  >>

    <<LAST_VP_OR_VS_A:x~time: --Assertions of state variables must also be invariant
      (vp@last_vp_or_vs or vs@last_vp_or_vs) and  --V-event occurred at last_vp_or_vs time
      not (exists t~time  --and not since
       in last_vp_or_vs,,x   --note open interval
       that (vs or vp)@t) >>
       
    <<LAST_AP_OR_AS_A:x~time: 
      (ap@last_ap_or_as or as@last_ap_or_as) and -- A-event occurred at last_ap_or_as
      not (exists t~time  --and not since
       in last_ap_or_as,,x   
       that (as or ap)@t) >>
       
invariant    
--invariant including internal variables
<<INV: : 
  LRL(now)   --lower rate limit property holds now, always
  and 
  VARS(now)  --variable invariant properties
  >>
  
  
variables
  last_vp_or_vs ~ time  --time of last ventricular pace or sense
  last_ap_or_as ~ time   --time of last atrial pace or sense
  va_interval ~ time constant --ventricular-atrial interval
  	:= #PP::Lower_Rate_Limit_Interval-#PP::Fixed_AV_Delay  --atrial paces following vs or vp
  	<<AXIOM_VA_INTERVAL: : va_interval = (#PP::Lower_Rate_Limit_Interval-#PP::Fixed_AV_Delay) >>;
	
states
  start : initial state  --first state, start pacing as if no sense or pace in previous LRL interval
    <<AS(now) and VS(now)>>; --first instant of operation
  off : final state;
  sav : complete state  --an atrial sense occurred 
    <<SAV(now) and INV() >>;    --  in the previous Sensed AV Delay
  sav_check_vrp : state  --ventricular sense during sensed AV delay
  	<<v@now and SAV(now) and INV() >>;  --was it in VRP? 
  sav_check_url : state  --SAV delay timeout, check for URL before tracking
    <<as@(now-#PP::Sensed_AV_Delay) and SAV(now) and INV() >>;
  pav : complete state  --an atrial pace occurred 
    <<PAV(now) and INV()>>;       --  in the previous Paced AV Delay
  pav_check_vrp : state  --ventricular sense during paced AV delay
  	<<v@now and PAV(now) and INV()>>;  --was it in VRP? 
  va : complete state   --a V pace or sense occurred in the previous VA interval
    <<INV()>>;   
  check_atrial_refractories : state   --check if atrial sense is in PVARP or ARP
    <<a@now and INV()>>;  
  check_vrp : state     --check if ventricular sense is premature ventricular contraction
    <<v@now and INV()>>;

transitions
  T0_GO:	--assume atrial and ventricular senses at start
  start -[ ]-> va 
    {<<AS(now) and VS(now)>> --first instant of operation
      vs!  <<vs@now>> 
      & 
      last_vp_or_vs:=now <<last_vp_or_vs=now>>
      &
      as! <<as@now>>
      &
      last_ap_or_as:=now <<last_ap_or_as=now>>
    }; 

  T1_PACE_AFTER_LRL:	--fundamental lower-rate pacing; keeps patients "pink"
  va,sav,pav -[on dispatch timeout (vp vs) #PP::Lower_Rate_Limit_Interval]->va
      { <<(vp or vs)@(now-#PP::Lower_Rate_Limit_Interval)
          and not (exists t~time 
           in (now-#PP::Lower_Rate_Limit_Interval),,now
           that (vp or vs)@t) and LAST_AP_OR_AS_A(now)
         and AXIOM_LRLi_gt_URLi_LIMIT(now)>>
       vp! 
         <<vp@now and not (exists t~time 
          in (now-#PP::Lower_Rate_Limit_Interval),,now
          that (vp or vs)@t) 
         and (vp or vs)@(now-#PP::Lower_Rate_Limit_Interval)
         and LAST_AP_OR_AS_A(now) and AXIOM_LRLi_gt_URLi_LIMIT(now)>>
       ;	--note sequential composition
       last_vp_or_vs:=now
       <<vp@last_vp_or_vs and vp@now  
         and (last_vp_or_vs=now) and LAST_AP_OR_AS_A(now)>>};

  T2_VS_AFTER_AS:    --ventricular sense during SAV delay
  sav -[on dispatch v]-> sav_check_vrp{};  --go check whether in VRP
  
  T3_VS_AFTER_AS_IN_VRP:  --ventricular sense during VRP, go back to sav
  sav_check_vrp -[last_vp_or_vs >= (now-#PP::Ventricular_Refractory_Period)]-> sav{};
  
  T4_VS_AFTER_AS_AFTER_VRP:  --ventricular sense after VRP, wait for atrial sense
  sav_check_vrp -[last_vp_or_vs < (now-#PP::Ventricular_Refractory_Period)]-> va
    {<<VS(now) and LAST_AP_OR_AS_A(now)>> 
    vs!
     <<vs@now and LAST_AP_OR_AS_A(now)>>
    & 
    last_vp_or_vs:=now <<last_vp_or_vs=now>>};   

  T5a_EXPIRED_SENSED_AV_DELAY:  --V-pace after sensed AV delay
    sav -[on dispatch timeout (as) #PP::Sensed_AV_Delay]-> sav_check_url{};
  
  T5b_TRACKED_VP_TOO_SOON_AFTER_PREVIOUS_VS_OR_VP:  --tracked VP too close to previous vp or vs
   sav_check_url -[last_vp_or_vs > (now-#PP::Upper_Rate_Limit_Interval)]-> sav{};
     --go back and wait for another AS to track, or for LRL to time-out forcing VP
   
  T5c_TRACKED_VP_AFTER_AS:  --v-pace after a-sense, if not exceeding URL
   sav_check_url -[last_vp_or_vs <= (now-#PP::Upper_Rate_Limit_Interval)]-> va
     {<<PACE_ON_SAV_DELAY(now) and VARS(now)
       and (last_vp_or_vs <= (now-#PP::Upper_Rate_Limit_Interval))>> 
    vp!  --ventricular pace tracking atrial sense
     <<vp@now and VARS(now) and AXIOM_LRLi_gt_URLi_LIMIT(now)>>
    & 
    last_vp_or_vs:=now <<last_vp_or_vs=now >>};
   
   
  T6_VS_AFTER_AP:  --ventricular sense after atrial pace
  pav -[on dispatch v]-> pav_check_vrp{};  --go check whether in VRP

  T7_VS_AFTER_AP_IN_VRP:  --vs too soon after previous vs or vp, go back to pav
  pav_check_vrp -[last_vp_or_vs >= (now-#PP::Ventricular_Refractory_Period)]-> pav{};  
  
  T8_VS_AFTER_AP_AFTER_VRP:  --ventricular sense after VRP, wait for atrial sense 
  pav_check_vrp -[ last_vp_or_vs < (now-#PP::Ventricular_Refractory_Period)]-> va  
    { <<VS(now) and VARS(now)>> 
      {
        <<VS(now) and LAST_AP_OR_AS_A(now)>>
      vs!
        <<vs@now and LAST_AP_OR_AS_A(now) and AXIOM_LRLi_gt_URLi_LIMIT(now)>>
      & 
      last_vp_or_vs:=now 
        <<last_vp_or_vs=now>>
      }
      <<vs@now and (last_vp_or_vs=now) and
        LAST_AP_OR_AS_A(now) and LAST_VP_OR_VS_A(now)>> };  
   
  T10_AS_AFTER_VS_OR_VP:   --a sense after v sense or v pace
  va -[on dispatch a]-> check_atrial_refractories{};  --is a too early?
  
  T11_PVARP_EXPIRED:  --a sense not in PVARP, start sensed A-V delay
  check_atrial_refractories -[((now-#PP::PVARP)>last_vp_or_vs)
  	 and ((now-#PP::Atrial_Refractory_Period)>last_ap_or_as)]-> sav  
    {<<AS(now) and LRL(now) and LAST_VP_OR_VS_A(now) and LAST_AP_OR_AS_A(now)>> 
    as!
     <<as@now and LRL(now) and LAST_VP_OR_VS_A(now) 
       and AXIOM_LRLi_gt_URLi_LIMIT(now) >>
    & 
    last_ap_or_as:=now 
     <<(last_ap_or_as=now) and LAST_AP_OR_AS_A(now)>>};
  
  T12_STILL_IN_PVARP:  --a sense in PVARP, go back to va
  check_atrial_refractories -[((now-#PP::PVARP)<=last_vp_or_vs)
  	or ((now-#PP::Atrial_Refractory_Period)<=last_ap_or_as)]-> va{}; 
  
  T13_VS_AFTER_VS_OR_VP:  --v sense after v sense or v pace
  va -[on dispatch v]-> check_vrp{};  
  
  T14_STILL_IN_VRP:  -- vs in VRP, go back to va
  check_vrp -[(now-#PP::Ventricular_Refractory_Period)<last_vp_or_vs]-> va{};  
    
  T15_VRP_EXPIRED:  --vs after VRP expired
  check_vrp -[(now-#PP::Ventricular_Refractory_Period)>=last_vp_or_vs]-> va
    {<<VS(now) and LAST_VP_OR_VS_A(now) and LAST_AP_OR_AS_A(now)>> 
    vs!
     <<vs@now and LAST_AP_OR_AS_A(now)
     >>
    & 
    last_vp_or_vs:=now 
     <<(last_vp_or_vs=now) and LAST_VP_OR_VS_A(now)>>};  
    
  T16_AP_AFTER_TIMEOUT_VS_OR_VP:  --atrial pace if no atrial sense during V-A interval
  va -[on dispatch timeout (vp vs) va_interval ]-> pav
    {{<<AXIOM_A_B4_V() and --last atrial event occurred before last ventricular event
       LAST_VP_OR_VS_A(now) and LAST_AP_OR_AS_A(now) and
       (vp or vs)@(now - va_interval) and
       not (exists tv~time
            in (now-va_interval),,now 
            that (vp or vs)@tv )>> 
    ap! 
     <<ap@now and LAST_VP_OR_VS_A(now) and LAST_AP_OR_AS_A(now) and
       not (exists tap~time
            in (now - va_interval),,now 
            that (vp or vs)@tap )>>
    &
      <<LRL(now) >>
    last_ap_or_as:=now
      <<(last_ap_or_as=now) and LRL(now) >> }
    <<(last_ap_or_as=now) and LAST_AP_OR_AS_A(now) and LAST_VP_OR_VS_A(now) and
     not (exists tv~time
          in (now - va_interval),,now 
          that (vp or vs)@tv ) 
     and LRL(now) and ap@now >>};  
    
  T17_STOP:  --turn off pacing
  sav,pav,va -[on dispatch stop]-> off{};  
  
**};	--end of annex subclause 

end DDD.i;

end ddd_mode;

--end of DDD.aadl
