package IoMT
public
	with BLESS, 
	BLESS_Types,
	IoMT_Properties,
	Physical_Types;


	
annex Assertion
{**
  ghost variables
		  def HEART_RATE ~ quantity bpm -- bpm beats per minute
		  def MINIMUM_HEART_RATE ~ quantity bpm
		  def SPO2 ~ quantity percent
		  def PROGRAMMED_SPO2 ~ quantity percent
		  def RESPIRATION_RATE ~ quantity brpm --brpm breaths per minute
		  def MINIMUM_RESPIRATION_RATE ~ quantity brpm 
		  def PATIENT_BOLUS_RATE ~ quantity mlph
		  def SQUARE_BOLUS_RATE ~ quantity mlph
		  def BASAL_RATE ~ quantity mlph
		  def LAST_ACTION_A ~ last_action 
		  def KVO_RATE_property ~ quantity mlph
		  def PAUSE_INFUSION ~ boolean 
		    --ANTLR-generated lexer won't handle "::" properly
		  << GIVEN_KVO_RATE: :KVO_RATE_property = #IoMT_Properties::KVO_Rate mlph >> -- can we use def for consistancy
		  
		  <<PO_ALARM: : HEART_RATE < MINIMUM_HEART_RATE or SPO2 < PROGRAMMED_SPO2 >>
		  <<RM_ALARM: : RESPIRATION_RATE < MINIMUM_RESPIRATION_RATE >>
		  <<HALT : :(LAST_ACTION_A = last_action'SafetyStopPump) or (LAST_ACTION_A = last_action'StopButton) or (LAST_ACTION_A = last_action'EndPriming)>>  --pump at 0 if stop button, or safety architecture says, or done priming
		  <<PB_RATE: :LAST_ACTION_A = last_action'PatientButton >>  --patient button pressed, and allowed
		  <<CCB_RATE: :(LAST_ACTION_A = last_action'StartSquareBolus) or (LAST_ACTION_A = last_action'ResumeSquareBolus)>>  --clinician-commanded bolus start or resumption after patient bolus
		  <<PRIME_RATE: :LAST_ACTION_A = last_action'StartPriming>>  --priming pump
		  <<BASAL_RATE_A: :(LAST_ACTION_A = last_action'StartButton) or (LAST_ACTION_A = last_action'ResumeBasal) or (LAST_ACTION_A = last_action'SquareBolusDone)>>  --regular infusion
		  
		 -- <<PCA_KVO_FAULT: : BASAL_OVERINFUSION or BOLUS_OVERINFUSION or SQUARE_OVERINFUSION or MAX_DOSE_WARNING or LOW_BATTERY>>
		  <<KVO_RATE: : not HALT() and (PO_ALARM() or RM_ALARM()) >> 
		
		  <<PUMP_RATE : returns quantity mlph := 
  			[
  			( HALT() )-> 0.0 mlph,                                 			--no flow
  			( KVO_RATE() )-> KVO_RATE_property,      			--KVO rate
 			( PB_RATE() )-> PATIENT_BOLUS_RATE,							--maximum infusion upon patient request
 		 	( CCB_RATE() )-> SQUARE_BOLUS_RATE,             			--square bolus rate=vtbi/duration, from data port
 	 		( PRIME_RATE() )-> #IoMT_Properties::Prime_Rate,  			--pump priming
  			( BASAL_RATE_A() )-> BASAL_RATE                   			--basal rate, from data port
  			]
  			>>
		 
		 <<IS_SAFE: : (HEART_RATE < MINIMUM_HEART_RATE
						or SPO2 < PROGRAMMED_SPO2 
						or RESPIRATION_RATE < MINIMUM_RESPIRATION_RATE)
			implies (PUMP_RATE$() = KVO_RATE_property or HALT()) >>
**};

	system SysIoMT
		properties
			BLESS::Invariant => "<< IS_SAFE() >>";
	end SysIoMT;

	system implementation SysIoMT.impl
		subcomponents
			pca_pump: system PCA_Pump;
			pulse_oximeter: system Pulse_Oximeter;
			resp_monitor: system Respiration_Monitor;
			supervisor: system ICE_Supervisor.impl;
			network: virtual bus Network_Bus;
			patient: abstract Patient;

		connections
			pause_infusion: port supervisor.Pause_Infusion -> pca_pump.Pause_infusion {Actual_Connection_Binding => (reference(network));};
			spo2_alarm: port pulse_oximeter.PO_Alarm -> supervisor.PO_Alarm {Actual_Connection_Binding => (reference(network));};
			resp_alarm: port resp_monitor.RM_Alarm -> supervisor.RM_Alarm {Actual_Connection_Binding => (reference(network));};
			infusion_set: feature pca_pump.Infused_Drug -> patient.Infused_Drug; --tube and needle between pump and patient
			
			network2pca: virtual bus access network <-> pca_pump.network_conn;
			po2network: virtual bus access network <-> pulse_oximeter.network_conn;
			rm2network: virtual bus access network <-> resp_monitor.network_conn;
			ca2network: virtual bus access network <-> supervisor.network_conn; 
		end SysIoMT.impl;
		
-- PCA Pump
	system PCA_Pump
		features
			Pause_infusion: in data port BLESS_Types::Boolean
		  	{BLESS::Assertion=>"<<PO_ALARM() or RM_ALARM()>>";};
			Infused_Drug: out feature Physical_Types::Fluid_Flow  --abstract AADL feature modeling drug delivery
				{BLESS::Assertion=> "<< returns quantity mlph:=PUMP_RATE() >>";};
			network_conn: requires virtual bus access Network_Bus;
			
		properties
			BLESS::Invariant =>
				"<< Pause_Infusion iff (PUMP_RATE$() = KVO_RATE_property or HALT()) >>";

	end PCA_Pump;
	
-- Pulse Oximeter
	system Pulse_Oximeter
		features
			PO_Alarm: out data port BLESS_Types::Boolean
			  {BLESS::Assertion=>"<< PO_ALARM() >>";};
			network_conn: requires virtual bus access Network_Bus;
		properties
			BLESS::Invariant=> "<< PO_Alarm iff PO_ALARM() >>";
	end Pulse_Oximeter;

-- Respiration Monitor
	system Respiration_Monitor
		features
			RM_Alarm: out data port BLESS_Types::Boolean
			  {BLESS::Assertion=>"<< RM_ALARM() >>";};
			network_conn: requires virtual bus access Network_Bus;
		properties
			BLESS::Invariant=> "<< RM_Alarm iff RM_ALARM() >>";
	end Respiration_Monitor;

-- Patient	
	abstract Patient
		features
			Infused_Drug: in feature Physical_Types::Fluid_Flow --abstract AADL feature modeling drug delivery
			  {BLESS::Value=> "<< returns quantity mlph := PUMP_RATE() >>";};
	end Patient;

	system ICE_Supervisor
		features
			PO_Alarm: in data port BLESS_Types::Boolean
		  	{BLESS::Assertion=>"<<PO_ALARM()>>";};
			RM_Alarm: in data port BLESS_Types::Boolean
	  		{BLESS::Assertion=>"<<RM_ALARM()>>";};
			Pause_Infusion: out data port BLESS_Types::Boolean
		  	{BLESS::Assertion=>"<<PO_ALARM() or RM_ALARM()>>";};
			network_conn: requires virtual bus access Network_Bus;
		properties
			BLESS::Invariant=> "<< (PO_ALARM() or RM_ALARM()) implies Pause_Infusion >>";		
	end ICE_Supervisor;
	
	system implementation ICE_Supervisor.impl
	subcomponents
		cc_proc: process Supervise_PCA_Process.impl;

	connections
		poa: port PO_Alarm -> cc_proc.PO_Alarm;
		rma: port RM_Alarm -> cc_proc.RM_Alarm;
		pi: port cc_proc.Pause_Infusion -> Pause_Infusion;
  end ICE_Supervisor.impl;
	
	process Supervise_PCA_Process
		features
			PO_Alarm: in data port BLESS_Types::Boolean
		  	{BLESS::Assertion=>"<<PO_ALARM()>>";};
			RM_Alarm: in data port BLESS_Types::Boolean
	  		{BLESS::Assertion=>"<<RM_ALARM()>>";};
			Pause_Infusion: out data port BLESS_Types::Boolean
		  	{BLESS::Assertion=>"<<PO_ALARM() or RM_ALARM()>>";};
		--	bus_access: requires bus access Network_bus;
		properties
			BLESS::Invariant=> "<< (PO_ALARM() or RM_ALARM()) implies Pause_Infusion >>";
	end Supervise_PCA_Process;
	
	process implementation Supervise_PCA_Process.impl
	subcomponents
		cc_app: thread Supervise_PCA_Thread.simple;
	connections
		poa: port PO_Alarm -> cc_app.PO_Alarm;
		rma: port RM_Alarm -> cc_app.RM_Alarm;
		pi: port cc_app.Pause_Infusion -> Pause_Infusion;
  end Supervise_PCA_Process.impl;

	thread Supervise_PCA_Thread
	 features
		Pause_Infusion : out data port BLESS_Types::Boolean
			{BLESS::Assertion=>"<<PO_ALARM() or RM_ALARM()>>";}; 
			
		PO_Alarm : in data port BLESS_Types::Boolean
			{BLESS::Assertion=>"<<PO_ALARM()>>";};
			
		RM_Alarm : in data port BLESS_Types::Boolean
			{BLESS::Assertion=>"<<RM_ALARM()>>";};
			
		Lost_Confirmation: out event port;  --confirmation of Pause_Infusion not arrived in time
		properties
			Dispatch_Protocol => Timed;
			Period => 500 ms;
	end Supervise_PCA_Thread;

--simple thread implementation without message confirmation	
	thread implementation Supervise_PCA_Thread.simple
		annex BLESS
			{**
				assert 
					<<GIVEN_PI: : PAUSE_INFUSION iff Pause_Infusion >>
				invariant 
					<<INV: : (PO_ALARM() or RM_ALARM()) implies Pause_Infusion >>
							
				states
					start: initial state <<INV() >>;
					run: complete state <<INV() >>;
					check: state <<INV() >>;
					done: final state;
					
				transitions
					go: start -[ ]-> run{};
					op: run -[on dispatch]-> check{};
					alarm: check -[PO_Alarm? or RM_Alarm?]-> run
						{Pause_Infusion!(true)};
					ok: check -[not PO_Alarm? and not RM_Alarm?]-> run
					 	{Pause_Infusion!(false)};
			**};
	end Supervise_PCA_Thread.simple;
		
	
	virtual bus Network_bus -- network can be any network, Ethernet, WLAN etc
		
	end Network_bus;
		
	
end IoMT;